import org.apache.tools.ant.filters.*

plugins {
  id "java-library"
  id "maven-publish"
  id "jacoco"
  id "com.github.spotbugs" version "6.0.27"
  id "org.sonarqube" version "6.0.1.5171"
  id "org.owasp.dependencycheck" version "12.0.0"
}

ext {
  interlokCoreVersion = project.findProperty('interlokCoreVersion') ?: '5.0-SNAPSHOT'
  releaseVersion = project.findProperty('releaseVersion') ?: '5.0-SNAPSHOT'
  nexusBaseUrl = project.findProperty('nexusBaseUrl') ?: 'https://nexus.adaptris.net/nexus'
  mavenPublishUrl = project.findProperty('mavenPublishUrl') ?: nexusBaseUrl + '/content/repositories/snapshots'
  javadocsBaseUrl = nexusBaseUrl + "/content/sites/javadocs/com/adaptris"
  repoUsername = project.findProperty('repoUsername') ?: 'unknown'
  repoPassword = project.findProperty('repoPassword') ?: 'unknown'
  defaultNexusRepo = project.findProperty('defaultNexusRepo') ?: 'unknown'
  interlokJavadocs= project.findProperty('interlokJavadocs') ?: javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.findProperty('interlokCommonJavadocs') ?: javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  offlineJavadocPackageDir = new File(project.buildDir, "offline-javadoc-packages")

  componentName='Interlok Scripting/JRuby'
  componentDesc="Explicit JRuby scripting capabilities rather than via JSR223"

  organizationName = "Adaptris Ltd"
  organizationUrl = "http://interlok.adaptris.net"
  slf4jVersion = '2.0.16'
  mockitoVersion = '5.2.0'
  jrubyVersion = '9.4.9.0'
}


ext.gitBranchNameOrTimestamp = { branchName ->
  if (branchName.equals("HEAD") || branchName.equals("develop") || branchName.startsWith("release")) {
    return new Date().format('HH:mm:ss z');
  }
  return branchName;
}

ext.buildInfo = { ->
   new ByteArrayOutputStream().withStream { os ->
      exec {
        executable = "git"
        args = ["rev-parse", "--abbrev-ref", "HEAD"]
        standardOutput = os
      }
      def branchName = os.toString().replaceAll("\r", "").replaceAll("\n", "").trim();
      return gitBranchNameOrTimestamp(branchName);
    }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
group   = 'com.adaptris'
version = releaseVersion
def versionDir = "$buildDir/version"

sonar {
  properties {
    property "sonar.projectKey", "adaptris_interlok-jruby"
    property "sonar.organization", "adaptris-org"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

// Disable gradle module generation since we probably don't want
// xxx.module files in your repository.
allprojects {
  tasks.withType(Jar) {
    metaInf {
      from ("$rootDir") {
        include 'LICENSE*'
        include 'NOTICE*'
      }
    }
  }
  tasks.withType(GenerateModuleMetadata) {
    enabled = false
  }
}

repositories {
  mavenCentral()
  if (defaultNexusRepo != "unknown") {
    maven {
      url "$defaultNexusRepo"
      allowInsecureProtocol true
    }
  }
  maven { url "$nexusBaseUrl/content/groups/public" }
  maven { url "$nexusBaseUrl/content/groups/interlok" }
  maven { url 'https://jitpack.io' }
}


configurations {
  javadoc {}
  offlineJavadocPackages {}
  all*.exclude group: 'c3p0'
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'javamail'
  all*.exclude group: 'javax.mail', module: 'mail'
  all*.exclude group: 'org.glassfish.hk2.external'
  all*.exclude group: 'xalan', module: 'xalan'
  all*.exclude group: 'net.sf.saxon', module: 'saxon'
  all*.exclude group: 'org.codehaus.woodstox'
  all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
  // INTERLOK-3197 exclude old javax.mail
  all*.exclude group: 'com.sun.mail', module: 'javax.mail'
  all*.exclude group: 'javax.validation', module: 'validation-api'
  all*.exclude group: 'javax.activation', module: 'activation'
  all*.exclude group: 'javax.activation', module: 'javax.activation-api'

  // INTERLOK-3740 switch from jcraft to com.github.mwiede jsch fork.
  all*.exclude group: 'com.jcraft', module: 'jsch'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {
  api ("com.adaptris:interlok-core:$interlokCoreVersion") { changing= true}
  api ("com.adaptris:interlok-common:$interlokCoreVersion") { changing= true}
  implementation ("org.slf4j:slf4j-api:$slf4jVersion")
  api ("org.jruby:jruby:$jrubyVersion")

  annotationProcessor ("com.adaptris:interlok-core-apt:$interlokCoreVersion") {changing= true}

  testImplementation ("org.junit.jupiter:junit-jupiter-engine:5.11.4")
  testImplementation ("org.slf4j:slf4j-simple:$slf4jVersion")
  testImplementation ("com.adaptris:interlok-stubs:$interlokCoreVersion") { changing= true }
  testImplementation ("org.mockito:mockito-core:$mockitoVersion")
  testImplementation ("org.mockito:mockito-inline:$mockitoVersion")

  javadoc("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-core:$interlokCoreVersion:javadoc@jar") { changing= true}
  offlineJavadocPackages ("com.adaptris:interlok-common:$interlokCoreVersion:javadoc@jar") { changing= true}
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}

sourceSets {
  main {
    output.dir(versionDir, builtBy: 'generateVersion')
  }
}

task configureJRuby {
  doLast {
    def jrubyZipFile = "jruby-dist-" + jrubyVersion + "-bin.zip"
    def jrubyUrl = "https://repo1.maven.org/maven2/org/jruby/jruby-dist/" + jrubyVersion + "/" + jrubyZipFile
    def downloadDest = project.buildDir.getCanonicalPath() + "/" + jrubyZipFile
    def jrubyBinDir =  project.buildDir.getCanonicalPath() + "/jruby-" + jrubyVersion + "/bin"
    ant.get(src: jrubyUrl, dest: downloadDest, usetimestamp: true)
    ant.unzip(src: downloadDest, dest: project.buildDir, overwrite:true)
    ant.exec(executable: "cmd", osfamily: "windows", dir: jrubyBinDir, failonerror: true) {
      arg(value: "/c")
      arg(value: "jruby.bat")
      arg(value: "-Sgem")
      arg(value: "install")
      arg(value: "jsonpath")
    }
    ant.exec(executable: "bash", osfamily: "unix", dir: jrubyBinDir, failonerror: true) {
      arg(value: "jruby")
      arg(value: "-Sgem")
      arg(value: "install")
      arg(value: "jsonpath")
    }
  }
}

task copyUnitTestProperties(type: Copy) {
    delete file('src/test/resources/unit-tests.properties')
    from file('src/test/resources/unit-tests.properties.template')
    into ('src/test/resources/')
    rename { String fileName ->
      fileName.replace('unit-tests.properties.template', 'unit-tests.properties')
    }
    filter(ReplaceTokens, tokens: [jrubyVersion: jrubyVersion])
}

task deleteUnitTestProperties(type: Delete) {
    delete file('src/test/resources/unit-tests.properties')
}

// Generate the META-INF/adaptris-version file
task generateVersion {
  doLast {
    def versionFile = new File(new File(versionDir, 'META-INF'), 'adaptris-version')
    versionFile.getParentFile().mkdirs()
    ant.propertyfile(file: versionFile) {
      entry(key: 'component.name', value: componentName)
      entry(key: 'component.description', value: componentDesc)
      entry(key: 'groupId', value: project.group)
      entry(key: 'artifactId', value: project.name)
      entry(key: 'build.version', value: project.version)
      entry(key: 'build.date', value: new Date().format('yyyy-MM-dd'))
      entry(key: 'build.info', value: buildInfo())
    }
  }
}

task offlinePackageList(type: Copy) {
  description 'Build an offline package-list for javadocs'
  group 'Documentation'

  project.configurations.offlineJavadocPackages.collect { dependency ->
    from(zipTree(dependency)) {
      into dependency.name
    }
  }
  include "package-list"
  include "element-list"
  into offlineJavadocPackageDir
}

javadoc {
  configure(options) {
    options.linksOffline(interlokJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-core-$interlokCoreVersion-javadoc.jar")
    options.linksOffline(interlokCommonJavadocs, offlineJavadocPackageDir.getCanonicalPath() + "/interlok-common-$interlokCoreVersion-javadoc.jar")
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "Xdoclint:none", "-quiet"
    options.addStringOption "tagletpath", configurations.javadoc.asPath
    title= componentName
  }
}

jacocoTestReport {
  reports {
    xml.required= true
    html.required= true
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

artifacts {
  archives javadocJar
  archives examplesJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }
      artifact sourcesJar { classifier "sources" }

      pom.withXml {
        asNode().appendNode("name", componentName)
        asNode().appendNode("description", componentDesc)
        asNode().appendNode("url", "https://interlok.adaptris.net/interlok-docs/#/")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.6.3+")
        properties.appendNode("tags", "ruby,jruby,scripting")
        properties.appendNode("license", "false")
        properties.appendNode("externalUrl", "http://jruby.org")
        properties.appendNode("repository", "https://github.com/adaptris/interlok-jruby")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}

spotbugsMain {
  effort = com.github.spotbugs.snom.Effort.valueOf("MAX")
  reportLevel = com.github.spotbugs.snom.Confidence.valueOf("HIGH")
  reports {
    xml {
      required.set(false)
    }
    html {
      required.set(true)
      stylesheet = 'default.xsl'
    }
  }
}

dependencyCheck  {
  suppressionFiles= [ "https://raw.githubusercontent.com/adaptris/interlok/develop/gradle/owasp-exclude.xml" , "$rootDir/gradle/owasp-exclude.xml" ]
  scanConfigurations = [ "api", "compileClasspath", "runtimeClasspath" ]
  formats = [ "HTML", "JUNIT" ]
  junitFailOnCVSS = 7.0
  failBuildOnCVSS = 7.0
  nvd {
    apiKey = System.getenv("NVD_API_KEY")
    delay=project.findProperty("dependencyCheckNvdDelay")
    datafeedUrl=project.findProperty("dependencyCheckNvdDatafeedUrl")
  }
  analyzers {
    assemblyEnabled=false
    knownExploitedURL=project.findProperty('dependencyCheckKnownExploitedURL')
  }
}
  
test {
  jvmArgs = ['--add-opens', 'java.base/java.lang=ALL-UNNAMED', '--add-opens', 'java.base/java.util=ALL-UNNAMED']
  useJUnitPlatform()
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false

check.dependsOn jacocoTestReport
processTestResources.dependsOn copyUnitTestProperties,configureJRuby
clean.dependsOn deleteUnitTestProperties
javadoc.dependsOn offlinePackageList
